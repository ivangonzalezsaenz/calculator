plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'checkstyle'
}

group = 'com.ivangonzalezsaenz'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.hazelcast:hazelcast:5.5.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.junit.platform:junit-platform-suite-api:1.13.3'
	testImplementation 'io.cucumber:cucumber-java:7.23.0'
	testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.23.0'
}

tasks.register('acceptanceTest', Test) {
  include '**/acceptance/**'
  systemProperties System.getProperties()

  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = sourceSets.test.runtimeClasspath
}

test {
  useJUnitPlatform()
  exclude '**/acceptance/**'
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.2
      }
    }
  }
}